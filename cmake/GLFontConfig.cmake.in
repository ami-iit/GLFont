set(GLFont_VERSION @GLFont_VERSION@)

include(CMakeFindDependencyMacro)

find_dependency(YCM REQUIRED)
find_dependency(GLEW REQUIRED)
find_dependency(glm REQUIRED)
## This is to select the newest version of OpenGL
if (POLICY CMP0072)
  cmake_policy (SET CMP0072 NEW)
endif(POLICY CMP0072)

find_dependency(PkgConfig REQUIRED)
pkg_check_modules(freetype2 REQUIRED IMPORTED_TARGET freetype2)

find_dependency(OpenGL REQUIRED)


if(NOT TARGET GLFont::GLFont)
  include("${CMAKE_CURRENT_LIST_DIR}/GLFontTargets.cmake")
endif()

get_filename_component(abs_font_path "${CMAKE_CURRENT_LIST_DIR}/../../../share/GLFont/fonts" ABSOLUTE)

## Manually edit the compile definitions to make sure that it finds the installed fonts instead of the source ones
set_target_properties(GLFont::GLFont PROPERTIES
  INTERFACE_COMPILE_DEFINITIONS "GLFont_DEFAULT_FONTS_PATH=\"${abs_font_path}\""
)
